/*
 * Copyright © 2015 Universidad Icesi
 * 
 * This file is part of the Amelia DSL.
 * 
 * The Amelia DSL is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or (at your
 * option) any later version.
 * 
 * The Amelia DSL is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public License
 * for more details.
 * 
 * You should have received a copy of the GNU Lesser General Public License
 * along with The Amelia DSL. If not, see <http://www.gnu.org/licenses/>.
 */
grammar org.amelia.dsl.Amelia with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate amelia "http://www.amelia.org/dsl/Amelia"

Model
	:	('package' name = QualifiedName ->';'?)?
		importSection = XImportSection?
		typeDeclaration = TypeDeclaration?
	;

TypeDeclaration
	:	SubsystemDeclaration
	|	DeploymentDeclaration
	;

DeploymentDeclaration
	:	extensions = ExtensionSection?
		'deployment' name = ID body = XBlockExpression
	;

SubsystemDeclaration returns Subsystem
	:	extensions = ExtensionSection?
		'subsystem' name = ID body = SubsystemBlockExpression
	;

ExtensionSection
	:	declarations += ExtensionDeclaration+
	;

ExtensionDeclaration
	:	DependDeclaration
	|	IncludeDeclaration
	;

IncludeDeclaration
	:	'includes' element = [TypeDeclaration | QualifiedName] ->';'?
	;

DependDeclaration
	:	'depends' 'on' element = [TypeDeclaration | QualifiedName] ->';'?
	;

SubsystemBlockExpression
	:	{SubsystemBlockExpression} '{' (expressions += InternalSubsystemDeclaration)* '}'
	;

InternalSubsystemDeclaration returns xbase::XExpression
	:	VariableDeclaration ->';'?
	|	OnHostBlockExpression
	|	ConfigBlockExpression
	;

VariableDeclaration
	:	{VariableDeclaration}
		(writeable?='var'|'val'|param?='param')
		(=> (type = JvmTypeReference name = ValidID) | name = ValidID) ('=' right = XExpression)?
	;

ConfigBlockExpression returns xbase::XBlockExpression
	:	{ConfigBlockExpression} 'config' '{' (expressions += XExpressionOrVarDeclaration ';'?)* '}'
	;

OnHostBlockExpression
	:	'on' hosts = XExpression '{' (rules += RuleDeclaration)* '}'
	;

RuleDeclaration
	:	name = ID ':' 
		(=> (dependencies += [RuleDeclaration | QualifiedName] (',' dependencies += [RuleDeclaration | QualifiedName])*)? ';')?
		(commands += XExpression)*
	;

CdCommand
	:	'cd' directory = XExpression (=> initializedLater ?= '...')?
	;

CompileCommand
	:	'compile' source = XExpression output = XExpression
		(=> '-classpath' classpath = XExpression)?
		(=> initializedLater ?= '...')?
	;


// TODO: turn command parameters into: 
// parameters += CommandParam (name = PARAM_NAME value = XExpression)
RunCommand
	:
		'run' (hasPort ?= '-r' port = XExpression)?
		composite = XExpression '-libpath' libpath = XExpression
		(=>
			hasService ?= ('-s' | '--service-name') service = XExpression
			hasMethod ?= ('-m' | '--method-name') method = XExpression
			(=> hasParams ?= '-p' params = XExpression)?
		)?
		(=> initializedLater ?= '...')?
	;

TransferCommand
	:	'transfer' source = XExpression 'to' destination = XExpression
	;

EvalCommand
	:	(=> 'on' uri = XExpression)? 'eval' script = XExpression
	;

CustomCommand
	:	'$' value = XExpression (=> initializedLater ?= '...')?
	;

CommandLiteral
	:	CdCommand
	|	CompileCommand
	|	CustomCommand
	|	EvalCommand
	|	RunCommand
	|	TransferCommand
	;

RichString
	:
		{RichString} (expressions += RichStringLiteral)
	| 	(
			expressions += RichStringLiteralStart 
			(expressions += XExpression (expressions += RichStringLiteralMiddle expressions += XExpression)*)
			expressions += RichStringLiteralEnd
		)
	;

RichStringLiteral
	:	{RichStringLiteral} value = RICH_TEXT
	;

RichStringLiteralStart
	:	{RichStringLiteral} value = RICH_TEXT_START
	;
	
RichStringLiteralMiddle
	:	{RichStringLiteral} value = RICH_TEXT_MIDDLE
	;
	
RichStringLiteralEnd
	:	{RichStringLiteral} value = RICH_TEXT_END
	;

XLiteral returns xbase::XExpression
	:	XCollectionLiteral
	|	XClosure
	|	XBooleanLiteral
	|	XNumberLiteral
	|	XNullLiteral
	|	XTypeLiteral
	|	XStringLiteral
	|	CommandLiteral
	|	RichString
	;

terminal RICH_TEXT
	:	"'" ('\\' . | !('\\' | "'" | '«' | '»') )* "'"
	;

terminal RICH_TEXT_START
	:	"'" ('\\' . | !('\\' | "'" | '«') )* '«'
	;

terminal RICH_TEXT_MIDDLE
	:	'»' ('\\' . | !('\\' | "'" | '«') )* '«'
	;

terminal RICH_TEXT_END
	:	'»' ('\\' . | !('\\' | "'" | '«') )* "'"
	;

terminal STRING
	:	'"' ( '\\' . /* ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') */ | !('\\'|'"') )* '"'?
	;
