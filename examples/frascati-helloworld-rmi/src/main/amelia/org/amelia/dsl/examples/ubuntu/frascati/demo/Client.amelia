package org.amelia.dsl.examples.ubuntu.frascati.demo

import org.amelia.dsl.lib.descriptors.Host
import org.amelia.dsl.lib.util.Log

depends on org.amelia.dsl.examples.ubuntu.frascati.FraSCAti
depends on org.amelia.dsl.examples.ubuntu.frascati.demo.Server

/*
 * Compiles and executes the helloworld-rmi Client component.
 *
 * @author Miguel Jimenez (miguel@uvic.ca)
 * @date 2017-09-22
 */
subsystem Client {

	/*
	 * The host to which the component is deployed.
	 */
	param Host host

	on host {
		configuration:
			cd '$FRASCATI_HOME/examples/helloworld-rmi'
			// ensure the right host name
			cmd 'sed -i \\"s/localhost/«host.hostname»/g\\" client/src/main/resources/helloworld-rmi-client.composite;'

		compilation:
			compile 'client/src' 'client'

		execution: compilation;
			(run 'helloworld-rmi-client' -libpath 'client.jar' -s 'r' -m 'run')
				.fetch [ Log.success(host, 'Client process running on PID «it»') ]
	}

	config {
		/*
		 * This is a special configuration to stop the Server component. As both
		 * subsystems use the same SSH connection, when the Client subsystem is
		 * shutdown, the connection is closed. For this reason, the Server component
		 * cannot be stopped when shutting the Server subsystem down.
		 */
		execute(true, false, true) // do not shutdown after execution
		releaseDependencies(dependencies) // shutdown dependencies
	}
}
